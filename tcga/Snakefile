from glob import glob

configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

fits = [ g[4:-2] for g in glob("fit_*.r") ]
#fits = [ 'rlm', 'rank' ]
types = [ "naive", "pur", "puradj" ]

mem = { "pan" : 61440, "BRCA" : 8192, "*" : 4096 }
cores = { "pan" : 10, "BRCA" : 5, "*" : 0 }

rule all:
    input:
        expand("{type}/{tissue}_{fit}/{sets}.pdf", tissue=config['tcga_tissues'],
                type=types, fit=fits, sets=['genes'])

rule fit:
    input:
        rscript = "fit_{fit}.r",
        sets = genesets("{sets}.rds")
    output:
        outfile = "{type}/{tissue}_{fit}/{sets}.xlsx"
    resources:
        mem = lambda wc: mem[wc.tissue] if wc.tissue in mem.keys() else mem["*"],
        cores = lambda wc: cores[wc.tissue] if wc.tissue in cores.keys() else cores["*"],
        walltime = 480
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --type {wildcards.type}"
            " --setfile {input.sets}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "../ccle/plot.r",
        infile = "{type}/{tissue}_{fit}/{sets}.xlsx"
    output:
        plotfile = "{type}/{tissue}_{fit}/{sets}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
