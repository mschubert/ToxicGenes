configfile: "../config.yaml"

subworkflow merge:
    workdir: "../merge"
    snakefile: "../merge/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

fits = ['rlm', 'rlm2', 'rlm3']

rule all:
    input:
#        "cor_overview.pdf", "rank_overview.pdf",
        expand("rank_top/{tissue}_{fit}.xlsx", tissue=config['cor_tissues'], fit=fits),
        expand("cor_dset/{tissue}_{fit}_{cna}.pdf",
                tissue=config['cor_tissues'], fit=fits, cna=['all', 'amp', 'del']),
        expand("gset_rlm3/{tissue}/{gset}.pdf",
                tissue=config['cor_tissues'], gset=config['genesets'])

rule rank:
    input:
        rscript = "rank_top.r",
        infile = merge("{tissue}.rds")
    output:
        outfile = "rank_top/{tissue}_{fit}.xlsx"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --fit {wildcards.fit}"
            " --outfile {output.outfile}"

rule cor:
    input:
        rscript = "cor_dset.r",
        dset = merge("{tissue}.rds")
    output:
        plotfile = "cor_dset/{tissue}_{fit}_{cna}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --cna {wildcards.cna}"
            " --fit {wildcards.fit}"
            " --plotfile {output.plotfile}"

rule overview:
    input:
        rscript = "{type}_overview.r",
        dset = merge("pan.rds")
    output:
        plotfile = "{type}_overview.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --plotfile {output.plotfile}"

rule genesets:
    input:
        rscript = "gset.r",
        dset = merge("{tissue}.rds"),
        setfile = genesets("{gset}.rds")
    output:
        plotfile = "gset_{fit}/{tissue}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --fit {wildcards.fit}"
            " --setfile {input.setfile}"
            " --plotfile {output.plotfile}"
