configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

types = [ "naive", "pur", "puradj" ]
tissues = ["pan", "BRCA", "LUAD", "LUSC", "HNSC", "KIRC", "SKCM", "COAD", "OV"]

mem = {
    "pan" : 61440,
    "BRCA" : 8192,
    "*" : 4096
}

rule all:
    input:
        expand("{type}/{tissue}_{fit}/{sets}.pdf", tissue=tissues, type=types,
                fit=['rlm', 'rank'], sets=config['genesets'] + ['genes'])

rule fit:
    input:
        rscript = "fit_{fit}.r",
        sets = genesets("{sets}.rds")
    output:
        outfile = "{type}/{tissue}_{fit}/{sets}.xlsx"
    resources:
        mem = lambda wc: mem[wc.tissue] if wc.tissue in mem.keys() else mem["*"],
        walltime = 480,
        cores = 5
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --type {wildcards.type}"
            " --setfile {input.sets}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "../ccle/plot.r",
        infile = "{type}/{tissue}_{fit}/{sets}.xlsx"
    output:
        plotfile = "{type}/{tissue}_{fit}/{sets}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
