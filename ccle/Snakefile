configfile: "../config.yaml"

subworkflow data_ccle:
    workdir: "../data/ccle"
    snakefile: "../data/ccle/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

tissues = ["pan", "BRCA", "LUAD", "SCLC", "SKCM", "COADREAD", "OV"]
#tissues = ["pan", "BRCA", "LUAD", "SKCM", "COADREAD", "ALL", "DLBC",
#           "BLCA", "KIRC", "HNSC", "LUSC", "LIHC", "OV", "PAAD",
#           "STAD", "UCEC", "GBM", "ESCA"]

mem = {
    'pan' : 4096,
    '*' : 2048
}

rule all:
    input:
        expand("{tissue}_{fit}/{sets}.pdf", tissue=tissues,
                fit=['rlm', 'rank'], sets=config['genesets'] + ['genes'])

rule data:
    input:
        expand("{tissue}_{fit}/{sets}.xlsx", tissue=tissues,
                fit=['rlm', 'rank'], sets=config['genesets'] + ['genes'])

rule fit:
    input:
        rscript = "fit_{fit}.r",
        infile = data_ccle("dset.rds"),
        setfile = genesets("{set}.rds")
    output:
        outfile = "{tissue}_{fit}/{set}.xlsx",
    resources:
        mem = lambda wc: [ mem[wc.tissue] if wc.tissue in mem.keys() else mem['*'] ][0],
        walltime = 240,
        cores = 0
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "{tissue}_{fit}/{set}.xlsx"
    output:
        plotfile = "{tissue}_{fit}/{set}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
