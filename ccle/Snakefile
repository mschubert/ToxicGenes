configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"

subworkflow genesets:
    workdir: "../data/genesets"

mem = { 'pan' : 1024, '*' : 512 }
cores = { 'pan' : 70, '*' : 0 }
time = { 'pan' : 72*60, '*' : 360 }

rule fits:
    input:
        expand("{tissue}/{fit}.pdf", tissue=config['ccle_tissues'], fit=config['fits']),
        expand("bionet/{tissue}.pdf", tissue=config['ccle_tissues'])

rule gsets:
    input:
        expand("{tissue}/{fit}/{gset}.pdf",
                tissue=config['cor_tissues'], gset=config['genesets'], fit=config['fits'])

rule fit:
    input:
        rscript = "fit_{fit}.r",
        infile = data("df_ccle.rds"),
    output:
        outfile = "{tissue}/{fit}.xlsx",
        outfile2 = "{tissue}/{fit}.rds"
    resources:
        mem = lambda wc: [ mem[wc.tissue] if wc.tissue in mem.keys() else mem['*'] ][0],
        cores = lambda wc: [ cores[wc.tissue] if wc.tissue in cores.keys() else cores['*'] ][0],
        walltime = lambda wc: [ time[wc.tissue] if wc.tissue in time.keys() else time['*'] ][0]
    group: "fit_and_plot"
    shell:
        "nice -n 10 Rscript {input.rscript}"
            " --infile {input.infile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "{tissue}/{fit}.xlsx"
    output:
        plotfile = "{tissue}/{fit}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule gset:
    input:
        rscript = "gset.r",
        infile = "{tissue}/{fit}.xlsx",
        setfile = genesets("{gset}.rds")
    output:
        plotfile = "{tissue}/{fit}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --plotfile {output.plotfile}"

rule bionet:
    input:
        rscript = "bionet.r",
        infile = "{tissue}/stan-nb.rds"
    output:
        plotfile = "bionet/{tissue}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
