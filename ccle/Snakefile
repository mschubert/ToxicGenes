configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"

subworkflow genesets:
    workdir: "../data/genesets"

mem = { 'pan' : 1024, '*' : 512 }
cores = { 'pan' : 70, '*' : 10 }
time = { 'pan' : 72*60, '*' : 360 }

rule fits:
    input:
        expand("fit_brms/{tissue}.pdf", tissue=config['ccle_tissues']),
        expand("bionet/{tissue}.pdf", tissue=config['ccle_tissues'])

rule gsets:
    input:
        expand("fit_brms/{tissue}/{gset}.pdf", tissue=config['cor_tissues'], gset=config['genesets'])

rule fit:
    input:
        rscript = "fit_{fit}.r",
        infile = data("df_ccle.rds")
    output:
        outfile = "fit_{fit}/{tissue}.rds"
    resources:
        mem = lambda wc: [ mem[wc.tissue] if wc.tissue in mem.keys() else mem['*'] ][0],
        cores = lambda wc: [ cores[wc.tissue] if wc.tissue in cores.keys() else cores['*'] ][0],
        walltime = lambda wc: [ time[wc.tissue] if wc.tissue in time.keys() else time['*'] ][0]
    group: "fit_and_plot"
    shell:
        "nice -n 10 Rscript {input.rscript}"
            " --infile {input.infile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "fit_{fit}/{tissue}.rds"
    output:
        plotfile = "plot_{fit}/{tissue}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule gset:
    input:
        rscript = "gset.r",
        infile = "{tissue}/{fit}.rds",
        setfile = genesets("{gset}.rds"),
    output:
        outfile = "plot_{fit}/{tissue}/{gset}.rds",
        plotfile = "plot_{fit}/{tissue}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule bionet:
    input:
        rscript = "bionet.r",
        infile = "fit_brms/{tissue}.rds"
    output:
        plotfile = "bionet/{tissue}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --n_genes 300"
            " --plotfile {output.plotfile}"
