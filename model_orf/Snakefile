configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"

rule all:
    input:
        expand("overview_{type}.pdf", type=['naive', 'corrected']),
        expand("fits_{type}.pdf", type=['naive', 'corrected']),
        "fits_per_screen.pdf",
        expand("{tissue}/{gset}.pdf", tissue=config['orf_tissues'],
                gset=['genes'])

rule overview_naive:
    input:
        rscript = "overview_naive.r",
        tissues = "../data/orf/tissues.txt",
        orflib = "../data/orf/20101003_ORF-size-plasmid.txt",
        orfscreen = "../data/orf/ORF_DMSO-ETP_2019-07.xlsx",
        orfscreen2 = "../data/orf/ORF_DMSO_2019-02.txt"
    output:
        outfile = "overview.rds",
        plotfile = "overview_naive.pdf"
    shell:
        "Rscript {input.rscript}"
            " --tissues {input.tissues}"
            " --orflib {input.orflib}"
            " --orfscreen {input.orfscreen}"
            " --orfscreen2 {input.orfscreen2}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule overview_corrected:
    input:
        rscript = "overview_corrected.r",
        infile = "overview.rds"
    output:
        plotfile = "overview_corrected.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

fields = {
    "naive" : "LFC DMSO/ETP",
    "corrected" : "z_LFC"
}

rule fits:
    input:
        rscript = "fits.r",
        infile = "overview.rds"
    params:
        field = lambda wc : fields[wc.type]
    output:
        outfile = "fits_{type}.xlsx",
        plotfile = "fits_{type}.pdf"
    wildcard_constraints:
        type = "[^_]+"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --field '{params.field}'"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule fits_per_screen:
    input:
        rscript = "fits_per_screen.r",
        infile = "overview.rds"
    params:
        field = lambda wc : fields['naive']
    output:
        outfile = "fits_per_screen.xlsx",
        plotfile = "fits_per_screen.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --field '{params.field}'"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule sets:
    input:
        rscript = "sets.r",
        infile = "overview.rds",
        setfile = genesets("{gset}.rds")
    output:
        outfile = "{tissue}/{gset}.xlsx",
        plotfile = "{tissue}/{gset}.pdf"
    wildcard_constraints:
        tissue = "[a-zA-Z0-9]+"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --tissue {wildcards.tissue}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"
