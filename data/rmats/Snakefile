comps = ['all_rbm_vs_luc', 'all_rbm8_vs_luc8', 'all_rbm24_vs_luc24',
         'H1650_rbm8_vs_luc8', 'H1650_rbm24_vs_luc24',
         'HCC70_rbm8_vs_luc8', 'HCC70_rbm24_vs_luc24',
         'H838_rbm8_vs_luc8', 'H838_rbm24_vs_luc24', 'H838_rbm72_vs_luc72']

rule all:
    input:
        "diff_expr.pdf",
        expand("splice/splice-{comp}.pdf", comp=comps),
        expand("splice/paired-{comp}.pdf", comp=comps[0:3])

rule rmats:
    input:
        sample1 = "{cline}_{sample1}.txt",
        sample2 = "{cline}_{sample2}.txt"
    output:
        tmpdir = directory("rmats_tmp/{paired}-{cline}_{sample1}_vs_{sample2}"),
        outdir = directory("rmats_out/{paired}-{cline}_{sample1}_vs_{sample2}")
    params:
        stats = lambda wc: ["--paired-stats" if wc.paired == "paired" else ""][0]
    shell: # https://github.com/Xinglab/rmats-turbo#all-arguments
        "python ~/rmats-turbo/rmats.py"
            " --b1 {input.sample1}"
            " --b2 {input.sample2}"
            " -t paired"
#            " --novelSS"
            " --gtf seqdata/hg38.refseq.gtf"
            " --readLength 150"
            " --allow-clipping"
            " --nthread 8"
            " {params.stats}"
            " --tmp {output.tmpdir}"
            " --od {output.outdir}"

rule eset:
    input:
        rscript = "eset.r"
    output:
        outfile = "eset.rds",
        plotfile = "eset.pdf"
    shell:
        "Rscript {input.rscript}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule diff_expr:
    input:
        rscript = "diff_expr.r",
        infile = "eset.rds"
    output:
        outfile = "diff_expr.rds",
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --outfile {output.outfile}"

rule de_plot:
    input:
        rscript = "diff_expr_plot.r",
        infile = "diff_expr.rds"
    output:
        plotfile = "diff_expr.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule splice:
    input:
        rscript = "splice.r",
        rmats = directory("rmats_out/{paired}-{comp}"),
        u12 = "GRCh38_U12.bed"
    output:
        outfile = "splice/{paired}-{comp}.rds",
    shell:
        "Rscript {input.rscript}"
            " --comp {wildcards.paired}-{wildcards.comp}"
            " --outfile {output.outfile}"

rule splice_plot:
    input:
        rscript = "{type}_plot.r",
        infile = "{type}/{paired}-{comp}.rds"
    output:
        plotfile = "{type}/{paired}-{comp}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule U12:
    output:
        "GRCh38_U12.bed",
        "GRCh38_U2.bed"
    shell:
        "wget https://introndb.lerner.ccf.org/static/bed/GRCh38_U12.bed && "
        "wget https://introndb.lerner.ccf.org/static/bed/GRCh38_U2.bed"
