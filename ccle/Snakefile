configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"

subworkflow genesets:
    workdir: "../data/genesets"

from collections import defaultdict as dd
mem = dd(lambda: 512, { 'pan' : 1024 })
cores = dd(lambda: 10, { 'pan' : 70 })
time = dd(lambda: 360, { 'pan' : 72*60 })

rule all:
    input:
        expand("plot_brms/{ccle_tissues}.pdf", **config),
        expand("plot_brms/{ccle_tissues}/{genesets}.pdf", **config),
        expand("bionet/{ccle_tissues}.pdf", **config)

rule fits:
    input:
        expand("fit_brms/{ccle_tissues}.rds", **config)

rule fit:
    input:
        rscript = "fit_{fit}.r",
        infile = data("df_ccle.rds")
    output:
        outfile = "fit_{fit}/{tissue}.rds"
    resources:
        mem = lambda wc: mem[wc.tissue],
        cores = lambda wc: cores[wc.tissue],
        walltime = lambda wc: time[wc.tissue]
    group: "fit_and_plot"
    shell:
        "nice -n 10 Rscript {input.rscript}"
            " --infile {input.infile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "fit_{fit}/{tissue}.rds"
    output:
        plotfile = "plot_{fit}/{tissue}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule gset:
    input:
        rscript = "gset.r",
        infile = "fit_{fit}/{tissue}.rds",
        setfile = genesets("{gset}.rds"),
    output:
        outfile = "plot_{fit}/{tissue}/{gset}.rds",
        plotfile = "plot_{fit}/{tissue}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule bionet:
    input:
        rscript = "bionet.r",
        infile = "fit_brms/{tissue}.rds"
    output:
        plotfile = "bionet/{tissue}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --n_genes 300"
            " --plotfile {output.plotfile}"
