from glob import glob

configfile: "../config.yaml"

subworkflow data_ccle:
    workdir: "../data/ccle"
    snakefile: "../data/ccle/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

mem = { 'pan' : 8192, '*' : 2048 }
cores = { 'pan' : 5, '*' : 0 }
time = { 'pan' : 960, '*' : 360 }

rule all:
    input:
        expand("{tissue}/{fit}.pdf", tissue=config['ccle_tissues'], fit=config['fits']),
        expand("{tissue}/{fit}/{gset}.pdf",
                tissue=config['cor_tissues'], gset=config['genesets'], fit=['rlm3', 'lm3']),

rule fit:
    input:
        rscript = "fit_{fit}.r",
        infile = data_ccle("dset.rds"),
    output:
        outfile = "{tissue}/{fit}.xlsx"
    resources:
        mem = lambda wc: [ mem[wc.tissue] if wc.tissue in mem.keys() else mem['*'] ][0],
        cores = lambda wc: [ cores[wc.tissue] if wc.tissue in cores.keys() else cores['*'] ][0],
        walltime = lambda wc: [ time[wc.tissue] if wc.tissue in time.keys() else time['*'] ][0]
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "{tissue}/{fit}.xlsx"
    output:
        plotfile = "{tissue}/{fit}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule gset:
    input:
        rscript = "gset.r",
        infile = "{tissue}/{fit}.xlsx",
        setfile = genesets("{gset}.rds")
    output:
        plotfile = "{tissue}/{fit}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --plotfile {output.plotfile}"
