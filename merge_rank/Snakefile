configfile: "../config.yaml"

subworkflow merge:
    workdir: "../merge"

subworkflow genesets:
    workdir: "../data/genesets"

rule all:
    input:
#        "rank_overview.pdf",
        expand("rank_top/{tissue}_{fit}.xlsx", tissue=config['cor_tissues'], fit=config['fits']+['lm']),
        expand("gset_{fit}/{tissue}/{gset}.pdf",
                tissue=config['cor_tissues'], gset=config['genesets'], fit=config['fits']),
        expand("bionet/{tissue}/{fit}.pdf",
                tissue=config['cor_tissues'], fit=config['fits']),
        expand("compare_genomic/{tissue}_{fit}.pdf",
                tissue=config['cor_tissues'], fit=config['fits'])

rule rank:
    input:
        rscript = "rank_top.r",
        infile = merge("{tissue}.rds")
    output:
        outfile = "rank_top/{tissue}_{fit}.xlsx"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --fit {wildcards.fit}"
            " --outfile {output.outfile}"

rule overview:
    input:
        rscript = "{type}_overview.r",
        dset = merge("pan.rds")
    output:
        plotfile = "{type}_overview.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --plotfile {output.plotfile}"

rule genesets:
    input:
        rscript = "gset.r",
        dset = merge("{tissue}.rds"),
        setfile = genesets("{gset}.rds")
    output:
        plotfile = "gset_{fit}/{tissue}/{gset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --fit {wildcards.fit}"
            " --setfile {input.setfile}"
            " --plotfile {output.plotfile}"

rule bionet:
    input:
        rscript = "bionet.r",
        ranks = "rank_top/{tissue}_{fit}.xlsx"
    output:
        plotfile = "bionet/{tissue}/{fit}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --fit {wildcards.fit}"
            " --ranks {input.ranks}"
            " --plotfile {output.plotfile}"

rule compare_genomic:
    input:
        rscript = "compare_genomic.r",
        ranks = "rank_top/{tissue}_{fit}.xlsx"
    output:
        plotfile = "compare_genomic/{tissue}_{fit}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --fit {wildcards.fit}"
            " --ranks {input.ranks}"
            " --plotfile {output.plotfile}"
