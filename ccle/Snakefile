configfile: "../config.yaml"

subworkflow data_ccle:
    workdir: "../data/ccle"
    snakefile: "../data/ccle/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

tissues = ["pan", "BRCA", "LUAD", "SCLC", "SKCM", "COADREAD", "OV"]
#tissues = ["pan", "BRCA", "LUAD", "SKCM", "COADREAD", "ALL", "DLBC",
#           "BLCA", "KIRC", "HNSC", "LUSC", "LIHC", "OV", "PAAD",
#           "STAD", "UCEC", "GBM", "ESCA"]

mem = {
    'pan' : 10240,
    '*' : 3072
}

rule all:
    input:
        expand("{tissue}/genes.pdf", tissue=tissues),
        expand("{tissue}/{sets}.pdf", tissue=tissues, sets=config['genesets'])

rule data:
    input:
        expand("{tissue}/genes.xlsx", tissue=tissues),
        expand("{tissue}/{sets}.xlsx", tissue=tissues, sets=config['genesets'])

rule fit_rlm:
    input:
        rscript = "fit_rlm.r",
        infile = data_ccle("dset.rds"),
        setfile = genesets("{set}.rds")
    output:
        outfile = "{tissue}/{set}.xlsx",
    resources:
        mem = lambda wc: [ mem[wc.tissue] if wc.tissue in mem.keys() else mem['*'] ][0],
        walltime = 240,
        cores = 10
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --setfile {input.setfile}"
            " --tissue {wildcards.tissue}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "{tissue}/{set}.xlsx"
    output:
        plotfile = "{tissue}/{set}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
