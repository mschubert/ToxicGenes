configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

types = [ "naive", "pur", "puradj" ]
tissues = ["pan", "BRCA", "LUAD", "LUSC", "HNSC", "KIRC", "SKCM", "COAD", "OV"]

mem = {
    "pan" : 61440,
    "BRCA" : 10240,
    "*" : 4096
}

rule all:
    input:
        expand("{type}/{tissue}/genes.pdf", tissue=tissues, type=types),
        expand("{type}/{tissue}/{sets}.pdf", tissue=tissues, type=types,
                sets=config['genesets'])

rule fit_rlm:
    input:
        rscript = "fit_rlm.r",
        sets = genesets("{sets}.rds")
    output:
        outfile = "{type}/{tissue}/{sets}.xlsx"
    resources:
        mem = lambda wc: mem[wc.tissue] if wc.tissue in mem.keys() else mem["*"],
        walltime = 120,
        cores = 10
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --type {wildcards.type}"
            " --setfile {input.sets}"
            " --cores {resources.cores}"
            " --memory {resources.mem}"
            " --outfile {output.outfile}"

rule plot:
    input:
        rscript = "../ccle/plot.r",
        infile = "{type}/{tissue}/{sets}.xlsx"
    output:
        plotfile = "{type}/{tissue}/{sets}.pdf"
    group: "fit_and_plot"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"
