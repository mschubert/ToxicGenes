configfile: "../config.yaml"

subworkflow orf:
    workdir: "../orf"
    snakefile: "../orf/Snakefile"

subworkflow ccle:
    workdir: "../ccle"
    snakefile: "../ccle/Snakefile"

subworkflow tcga:
    workdir: "../tcga"
    snakefile: "../tcga/Snakefile"

import glob
manual = [ x[:-5] for x in glob.glob("*.yaml") ]

fits = ['rlm', 'rlm2', 'rlm3']
types = ['naive', 'pur', 'puradj']

rule all:
    input:
        expand("{tissue}.rds", tissue=config['cor_tissues']),
        expand("cor_dset/{tissue}_{fit}_{cna}.pdf",
                tissue=config['cor_tissues'], fit=fits, cna=['all', 'amp', 'del']),

rule merge_one:
    input:
        rscript = "merge.r",
        orf = lambda wc: orf(wc.tissue + "/genes.xlsx"),
        ccle = lambda wc: ccle(expand(wc.tissue + "/{fit}.xlsx", fit=fits)),
        tcga = lambda wc: tcga(expand(wc.tissue + "/{fit}_{type}.xlsx", fit=fits, type=types))
    output:
        outfile = "{tissue}.rds"
    shell:
        "Rscript {input.rscript}"
            " --tissue {wildcards.tissue}"
            " --outfile {output.outfile}"
            " --plotfile {output.outfile}"

rule cor:
    input:
        rscript = "cor_dset.r",
        dset = "{tissue}.rds"
    output:
        plotfile = "cor_dset/{tissue}_{fit}_{cna}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --cna {wildcards.cna}"
            " --fit {wildcards.fit}"
            " --plotfile {output.plotfile}"

rule overview:
    input:
        rscript = "{type}_overview.r",
        dset = "pan.rds"
    output:
        plotfile = "{type}_overview.pdf"
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --plotfile {output.plotfile}"
